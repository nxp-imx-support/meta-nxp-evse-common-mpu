DESCRIPTION = "An i.MX EasyEVSE MPU image with ROS Core Linux system functionality installed."

LICENSE = "MIT"

require ../../../meta-imx/meta-sdk/dynamic-layers/qt6-layer/recipes-fsl/images/imx-image-full.bb

IMAGE_INSTALL += " packagegroup-cloud-azure cjson cjson-dev ros-base libnfceasyevse libnfceasyevse-dev sevenstax-stack sevenstax-v2g easyevse-ros-demo easyevse-ros-demo-interfaces easyevse-ros-helpers func-timeout"

# Security-related packages
IMAGE_INSTALL += " \
    se05x \
    opensc \
    p11-kit \
    softhsm \
    "

ROOTFS_POSTPROCESS_COMMAND:append:mx93-nxp-bsp = " \
    install_demo; \
    install_demo_easyevse; \
    prepare_sigb_network_interface; \
    configure_security; \
    ${@bb.utils.contains('DISTRO_FEATURES', 'LVDS_DISPLAY_SUPPORT', 'calibrate_lvds;', '', d)} \
    "

ROOTFS_POSTPROCESS_COMMAND:append:mx8-nxp-bsp = " \
    install_demo; \
    install_demo_easyevse; \
    prepare_sigb_network_interface; \
    configure_security; \
    "

install_demo_easyevse() {
	printf "\n\n[output]\nname=DSI-1\nmode=1920x1080@60\ntransform=rotate-270" >> ${IMAGE_ROOTFS}${sysconfdir}/xdg/weston/weston.ini
}

install_demo() {
    if ! grep -q "HOME=/home/root/" ${IMAGE_ROOTFS}${sysconfdir}/default/weston
    then
        printf "\nHOME=/home/root/\nQT_QPA_PLATFORM=wayland" >> ${IMAGE_ROOTFS}${sysconfdir}/default/weston
    fi
}

prepare_sigb_network_interface() {
	rm ${IMAGE_ROOTFS}${sysconfdir}/resolv.conf
	ln -sf /etc/resolv-conf.systemd ${IMAGE_ROOTFS}${sysconfdir}/resolv.conf
	touch ${IMAGE_ROOTFS}${sysconfdir}/systemd/network/20-eth0.network
	printf "[Match]\nName=eth0\nKernelCommandLine=!nfsroot\n[Network]\nAddress=169.254.0.10/16" >> ${IMAGE_ROOTFS}${sysconfdir}/systemd/network/20-eth0.network
	touch ${IMAGE_ROOTFS}${sysconfdir}/systemd/network/21-wireless.network
	printf "[Match]\nName=wfd0\n[Network]\nDHCP=ipv4\nLinkLocalAddressing=no\n[DHCP]\nRouteMetric=20" >> ${IMAGE_ROOTFS}${sysconfdir}/systemd/network/21-wireless.network
	touch ${IMAGE_ROOTFS}${sysconfdir}/systemd/network/22-wireless.network
	printf "[Match]\nName=mlan0\n[Network]\nDHCP=ipv4\nLinkLocalAddressing=no\n[DHCP]\nRouteMetric=20" >> ${IMAGE_ROOTFS}${sysconfdir}/systemd/network/22-wireless.network
	touch ${IMAGE_ROOTFS}${sysconfdir}/modprobe.d/wifi.conf
	printf "options moal mod_para=nxp/wifi_mod_para.conf" >> ${IMAGE_ROOTFS}${sysconfdir}/modprobe.d/wifi.conf
	touch ${IMAGE_ROOTFS}${sysconfdir}/modules-load.d/wifi.conf
	printf "moal" >> ${IMAGE_ROOTFS}${sysconfdir}/modules-load.d/wifi.conf
	mkdir ${IMAGE_ROOTFS}${sysconfdir}/wpa_supplicant
	touch ${IMAGE_ROOTFS}${sysconfdir}/wpa_supplicant/wpa_supplicant-wfd0.conf
	printf "ctrl_interface=/var/run/wpa_supplicant\nctrl_interface_group=0\nupdate_config=1\nap_scan=1\n\nnetwork={\n	key_mgmt=WPA-PSK\n	ssid=\"NAME-OF-YOUR-NETWORK\"\n	psk=\"PASSWORD\"\n}\n" >> ${IMAGE_ROOTFS}${sysconfdir}/wpa_supplicant/wpa_supplicant-wfd0.conf
	touch ${IMAGE_ROOTFS}${sysconfdir}/wpa_supplicant/wpa_supplicant-mlan0.conf
	printf "ctrl_interface=/var/run/wpa_supplicant\nctrl_interface_group=0\nupdate_config=1\nap_scan=1\n\nnetwork={\n	key_mgmt=WPA-PSK\n	ssid=\"NAME-OF-YOUR-NETWORK\"\n	psk=\"PASSWORD\"\n}\n" >> ${IMAGE_ROOTFS}${sysconfdir}/wpa_supplicant/wpa_supplicant-mlan0.conf

}

configure_security() {
    PKCS11_MODULES_PATH=${IMAGE_ROOTFS}${datadir}/p11-kit/modules

    mkdir -p ${PKCS11_MODULES_PATH}

    # Only system configuration, ignore user configuration
    echo "user-config: none" >> ${IMAGE_ROOTFS}${sysconfdir}/pkcs11/pkcs11.conf

    # Remove any unneeded preconfigured module
    rm -rf ${PKCS11_MODULES_PATH}/*

    # SE05x
    echo "module: /usr/lib/libsss_pkcs11.so" >> ${PKCS11_MODULES_PATH}/sss_pkcs11.module
    echo "priority: 10" >> ${PKCS11_MODULES_PATH}/sss_pkcs11.module
    echo "critical: yes" >> ${PKCS11_MODULES_PATH}/sss_pkcs11.module

    # TEE
    echo "module: /usr/lib/libckteec.so.0" >> ${PKCS11_MODULES_PATH}/libckteec.module
    echo "priority: 3" >> ${PKCS11_MODULES_PATH}/libckteec.module
    echo "critical: no" >> ${PKCS11_MODULES_PATH}/libckteec.module
    echo "enable-in: stx_evse_*" >> ${PKCS11_MODULES_PATH}/libckteec.module

    # SoftHSMv2
    echo "module: /usr/lib/softhsm/libsofthsm2.so" >> ${PKCS11_MODULES_PATH}/softhsm2.module
    echo "priority: 2" >> ${PKCS11_MODULES_PATH}/softhsm2.module
    echo "critical: no" >> ${PKCS11_MODULES_PATH}/softhsm2.module
    echo "enable-in: stx_evse_*" >> ${PKCS11_MODULES_PATH}/softhsm2.module
}

calibrate_lvds() {
	if [ ! -f "${IMAGE_ROOTFS}${sysconfdir}//udev/rules.d/touchscreen.rules" ]
	then
		touch ${IMAGE_ROOTFS}${sysconfdir}//udev/rules.d/touchscreen.rules
		echo '# Create a symlink to any touchscreen input device' >> ${IMAGE_ROOTFS}${sysconfdir}//udev/rules.d/touchscreen.rules
		echo 'SUBSYSTEM=="input", KERNEL=="event[0-9]*", ATTRS{modalias}=="input:*-e0*,3,*a0,1,*18,*", SYMLINK+="input/touchscreen0"' >> ${IMAGE_ROOTFS}${sysconfdir}//udev/rules.d/touchscreen.rules
		echo 'SUBSYSTEM=="input", KERNEL=="event[0-9]*", ATTRS{modalias}=="ads7846", SYMLINK+="input/touchscreen0"' >> ${IMAGE_ROOTFS}${sysconfdir}//udev/rules.d/touchscreen.rules
		echo '# i.MX specific touchscreen rules' >> ${IMAGE_ROOTFS}${sysconfdir}//udev/rules.d/touchscreen.rules
		echo 'SUBSYSTEM=="input", KERNEL=="event[0-9]*", ENV{ID_INPUT_TOUCHSCREEN}=="1", SYMLINK+="input/touchscreen0"' >> ${IMAGE_ROOTFS}${sysconfdir}//udev/rules.d/touchscreen.rules
	fi
	echo '# LVDS calibration matrix' >> ${IMAGE_ROOTFS}${sysconfdir}//udev/rules.d/touchscreen.rules
	echo 'SUBSYSTEM=="input", KERNEL=="event[0-9]*", ENV{ID_INPUT_TOUCHSCREEN}=="1",ENV{LIBINPUT_CALIBRATION_MATRIX}="4.034244 -0.004270 -0.004517 -0.016081 4.132606 -0.011880"' >> ${IMAGE_ROOTFS}${sysconfdir}//udev/rules.d/touchscreen.rules
}
